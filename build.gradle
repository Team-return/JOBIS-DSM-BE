plugins {
    id "java"
    id "org.sonarqube" version "4.4.1.3373"
    id "org.openrewrite.rewrite" version "6.10.0"
}

repositories {
    mavenCentral()
}

rewrite {
    activeRecipe("org.openrewrite.java.RemoveUnusedImports")
    activeRecipe("org.openrewrite.java.ShortenFullyQualifiedTypeReferences")
    activeRecipe("org.openrewrite.staticanalysis.CodeCleanup")
    activeRecipe("org.openrewrite.java.OrderImports")
    activeRecipe("org.openrewrite.java.format.AutoFormat")

    checkstyleConfigFile = file("${project.rootDir}/jobis-lint.xml")
    exclusion("**/entity/Q*.java")
}

subprojects {
    apply plugin: "java"
    apply plugin: "checkstyle"

    repositories {
        mavenCentral()
    }

    checkstyle {
        toolVersion = "8.45"
        configFile = file("${project.rootDir}/jobis-lint.xml")
        ignoreFailures = false
        maxWarnings = 0
    }

    checkstyleMain {
        source = sourceSets.main.allSource
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:1.18.30"
        annotationProcessor "org.projectlombok:lombok:1.18.30"
    }

    test.useJUnitPlatform()
}

allprojects {
    apply plugin: "jacoco"
    apply plugin:  "checkstyle"

    group = 'team.retum'
    version = '1.0.4'

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_17.majorVersion
    }

    test {
        useJUnitPlatform()
    }
}

dependencies {
    rewrite("org.openrewrite.recipe:rewrite-static-analysis:1.4.0")
}

sonar {
    properties {
        property "sonar.projectKey", "Team-Return_JOBIS-DSM-BE"
        property "sonar.organization", "team-return"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

jar.enabled = false
