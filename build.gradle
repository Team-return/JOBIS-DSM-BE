plugins {
    id "java"
    id "org.sonarqube" version "4.4.1.3373"
    id "org.openrewrite.rewrite" version "6.10.0"
}

repositories {
    mavenCentral()
}

subprojects {
    apply plugin: "java"
    apply plugin: "checkstyle"

    repositories {
        mavenCentral()
    }

    checkstyle {
        toolVersion = "10.8.1"
        configFile = file("${project.rootDir}/jobis-lint.xml")
        ignoreFailures = false
        maxWarnings = 0
    }

    rewrite {
        activeRecipe("org.openrewrite.java.RemoveUnusedImports")
        activeRecipe("org.openrewrite.java.format.BlankLines")
        activeRecipe("org.openrewrite.java.ShortenFullyQualifiedTypeReferences")
        activeRecipe("org.openrewrite.staticanalysis.CodeCleanup")
        activeRecipe("org.openrewrite.java.OrderImports")

        checkstyleConfigFile = file("${project.rootDir}/jobis-lint.xml")
    }

    checkstyleMain {
        source = sourceSets.main.allSource
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:1.18.30"
        annotationProcessor "org.projectlombok:lombok:1.18.30"
    }

    test.useJUnitPlatform()
}

allprojects {
    apply plugin: "jacoco"
    apply plugin:  "checkstyle"

    group = 'team.retum'
    version = '1.0.4'

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_17.majorVersion
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    test {
        useJUnitPlatform()
    }
}

dependencies {
    rewrite("org.openrewrite.recipe:rewrite-static-analysis:1.4.0")
    rewrite("org.openrewrite:rewrite-java")
}

sonar {
    properties {
        property "sonar.projectKey", "Team-Return_JOBIS-DSM-BE"
        property "sonar.organization", "team-return"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.cpd.exclusions", "**/entity/**,**/model/**"
    }
}

jar.enabled = false
